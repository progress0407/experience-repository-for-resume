import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.1'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.jetbrains.kotlin.jvm' version '1.9.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.9.21'
}

bootJar.enabled = false
jar.enabled = true

allprojects {
	group = 'io.philo'
	version = '0.0.1-SNAPSHOT'

	java {
		sourceCompatibility = javaVersion
	}

	ext {
		set('springCloudVersion', "2023.0.0")
	}

	repositories {
		mavenCentral()
	}
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.jetbrains.kotlin.plugin.spring'

	bootJar.enabled = true
	jar.enabled = true

    dependencies {
        // kotlin
        implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'
        implementation 'io.github.microutils:kotlin-logging:3.0.5'

        // JVM Test
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'

        testImplementation "io.kotest:kotest-runner-junit5:5.6.2"
        testImplementation "io.kotest:kotest-assertions-core:5.6.2"

        // spring
        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'

//        implementation 'org.springframework.boot:spring-boot-starter-web' // tomcat
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    tasks.withType(KotlinCompile) {
        kotlinOptions {
            freeCompilerArgs += '-Xjsr305=strict'
            jvmTarget = javaVersion
        }
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(":gateway-router-test") {
	dependencies {
		implementation 'org.springframework.cloud:spring-cloud-starter-gateway' // netty
	}
}
